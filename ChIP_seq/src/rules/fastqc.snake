# This runs fastqc on the files from the samples document can be single or paired end

# Function to return paths of input files
def _get_input(wildcards):
    # Grab path of the fastq file
    fastq1 = SAMPLE_LIST.loc[wildcards.sample, "fastq1"]
    data_dir = SAMPLE_LIST.loc[wildcards.sample, "data_dir"]
    fastq1 = data_dir + "/" + fastq1
    # Make sure file exists
    fastq1  = _check_path(fastq1)
    if IS_PAIRED:
        # Grab path of second read
        fastq2 = SAMPLE_LIST.loc[wildcards.sample, "fastq2"]
        fastq2 = data_dir + "/" + fastq2
        # Make sure file exists
        fastq2 = _check_path(fastq2)
        return(fastq1, fastq2)
    else:
        return(fastq1)

# Function to return paths of input files
def _get_directories(wildcards):
    # Grab path of the fastq file
    fastq1 = SAMPLE_LIST.loc[wildcards.sample, "fastq1"]
    path = wildcards.results + "/fastqc/"
    data_dir1 = re.sub(r'\.fastq\.gz', '_fastqc.zip', fastq1)
    data_dir1 = path + data_dir1
    if IS_PAIRED:
        # Grab path of second read
        fastq2 = SAMPLE_LIST.loc[wildcards.sample, "fastq2"]
        data_dir2 = re.sub(r'\.fastq\.gz', '_fastqc.zip', fastq2)
        data_dir2 = path + data_dir2
        return(data_dir1, data_dir2)
    else:
        return(data_dir1)

rule fastqc:
    input:
        input_list = _get_input
    output:
        file      = "{results}/fastqc/fastqc_{sample}_summary.txt"
    params:
        output_dir  = "{results}/fastqc/",
        sample_name = "{sample}",
        job_name    = "{sample}_fastqc",
        memory      = "select[mem>16] rusage[mem=16]",
        directories = _get_directories
    log:
        "{results}/logs/fastqc_{sample}"
    shell:
        """
        fastqc {input} --outdir {params.output_dir}
        for dir in {params.directories};
        do
            name=$(basename -s .zip $dir)

            unzip -p $dir $name/summary.txt \
                >> {output}
        done
        """    

# Combine FastQC summaries
rule fastqc_summary:
    input:
        expand(
            "{results}/fastqc/fastqc_{sample}_summary.txt",
            results = RESULTS, sample = SAMPLES
        )
    output:
        "{results}/fastqc_summary.tsv"
    params:
        job_name = "fastqc_summary",
        memory   = "select[mem>4] rusage[mem=4]"
    log:
        "{results}/logs/fastqc/summary"
    message:
        "Creating FastQC summary"
    threads:
        1
    shell:
        """
        cat {input} > {output}
        """